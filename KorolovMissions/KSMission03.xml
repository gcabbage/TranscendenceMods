<?xml version="1.0" ?>

<TranscendenceModule>

	<!-- Mission: Kill Stronghold ==============================================

	The standard Korolov kill stronghold mission rewritten using MissionType

	EXTRA DATA

	reward:			Reward (in credits) for completing mission
	missionXP:		XP awarded for completing mission

	targetID:		Primary Charon stronghold
	leaderID:		Leader of attack squadron

	director:		Name of Korlov station director
	dist:			Distance to stronghold

	leaderLost:		True if the squadron leader is destroyed
	betrayal:		True if the player kills the squadron leader
	noHelp:			True if the player didnot help (more than 300 ls away when station destroyed)
	commend:		True if the player destroyed the stronghold

	alreadyDeclined:	True if the player has seen the initial DeclineReply

	state:		One of the following:
		Nil:
		declined:	Player declined mission
		catchup:	Player declined mission, then changed their mind
		accepted:	Player accepted mission immediately

	======================================================================== -->

	<MissionType UNID="&msKorolovKillStronghold;"
			name=			"Kill Stronghold"
			attributes=		"korolov"

			level=			"1-3"
			failureAfterOutOfSystem="5400"
			>

		<Events>
			<OnCreate>
				(block (targetObj)
					(switch
						; Player has to complete certain number of escort missions first
						(gr (objGetData aOwnerObj 'remainingMissions) 0)
							(msnDestroy gSource)

						; Only offer to levels 1-3
						(not (find '(1 2 3) (korGetPlayerLevel)))
							(msnDestroy gSource)

						; Only offer once per station
						(msnFind aOwnerObj "*S +unid:&msKorolovKillStronghold;;")
							(msnDestroy gSource)

						; Find Charon stronghold
						(not (setq targetObj (sysFindObject aOwnerObj "TAN +charonPirates; +primaryStronghold;")))
							(msnDestroy gSource)

						(block Nil
							(msnSetData gSource 'targetID (objGetID targetObj))
							(msnRegisterForEvents gSource targetObj)
							(msnRegisterForEvents gSource gPlayerShip)
							(msnSetData gSource 'missionXP 100)
							(msnSetData gSource 'reward (@ '(0 1000 2000 4000 8000) (sysGetLevel)))
							; Data for briefing
							(msnSetData gSource 'director (objGetData aOwnerObj 'assistantDirector))
							(msnSetData gSource 'dist (objGetDistance aOwnerObj targetObj))
							)
						)
					)
			</OnCreate>

			<OnAccepted>
				(if (msnGetData gSource 'state)
					; If we've already sent out a squadron the player has to catch up
					(block (
						(leaderObj (objGetObjByID (msnGetData gSource 'leaderID)))
						)
						; Update the leader so we recieve messages
						(objSetData leaderObj 'wingmanPlayer True)
						; Reduce XP and reward
						(msnIncData gSource 'missionXP -50)
						(msnSetData gSource 'reward (multiply (sysGetLevel) 200))
						(msnSetData gSource 'state 'catchup)
						)

					; Otherwise send out a squadron
					(block (
						(stationObj (objGetObjByID (msnGetProperty gSource 'ownerID)))
						(targetObj (objGetObjByID (msnGetData gSource 'targetID)))
						leaderObj leaderClass
						)
						; If the player is slow, then we pick a slow leader
						(setq leaderClass
							(if (ls (shpGetMaxSpeed gPlayerShip) 20)
								&scEI200CharonBuster;
								&scRoninCharonBuster;
							))
						(setq leaderObj (sysCreateShip leaderClass stationObj &svCommonwealth;))
						(msnSetData gSource 'leaderID (objGetID leaderObj))
						(msnRegisterForEvents gSource leaderObj)

						(setq aTargetObj targetObj)
						(setq aBaseObj stationObj)
						(setq aWingmanPlayer True)
						(objFireEvent leaderObj "OrderDestroyStation")
						(msnSetData gSource 'state 'accepted)
						)
					)
			</OnAccepted>

			<OnDeclined>
				(if (msnGetData gSource 'state)
					; If we've already sent out a squadron, just update the state so we can display a different message
					(msnSetData gSource 'alreadyDeclined True)

					; Otherwise send out the squadron
					(block (
						(stationObj (objGetObjByID (msnGetProperty gSource 'ownerID)))
						(targetObj (objGetObjByID (msnGetData gSource 'targetID)))
						leaderObj
						)
						(setq leaderObj (sysCreateShip &scRoninCharonBuster; stationObj &svCommonwealth;))
						(msnSetData gSource 'leaderID (objGetID leaderObj))
						(msnRegisterForEvents gSource leaderObj)

						(setq aTargetObj targetObj)
						(setq aBaseObj stationObj)
						(setq aWingmanPlayer Nil)
						(objFireEvent leaderObj "OrderDestroyStation")
						(msnSetData gSource 'state 'declined)
						)
					)
			</OnDeclined>

			<OnSetPlayerTarget>
				(block (
					(leaderObj (objGetObjByID (msnGetData gSource 'leaderID)))
					(targetObj (objGetObjByID (msnGetData gSource 'targetID)))
					)
					(switch
						(and leaderObj (= (objGetData leaderObj 'status) 'attackingTarget))
							(rpgSetTarget gSource aReason targetObj)

						leaderObj
							(rpgSetTarget gSource aReason leaderObj 'escort)

						(rpgSetTarget gSource aReason targetObj)
						)
					)
			</OnSetPlayerTarget>

			<OnCompleted>
				(block (
					(stationObj (objGetObjByID (msnGetProperty gSource 'ownerID)))
					)
					(if (= aReason 'failure)
						(objSetData stationObj 'charonDominates True)
						)
					(switch
						; Player is blacklisted if they fail the mission and kill the squadron
						(and (= aReason 'failure) (msnGetData gSource 'betrayal))
							(block Nil
								(typSetData &unidKorolovEscort; 'level -1)
								(typSetData &unidKorolovEscort; 'xp -1)
								)
						)
					)
			</OnCompleted>

			<OnReward>
				(korGiveReward gSource)
			</OnReward>

			<OnObjDestroyed>
				(switch
					(= aDestroyReason 'enteredStargate)
						Nil
					(= (objGetID aObjDestroyed) (msnGetData gSource 'leaderID))
						(block Nil
							; If the squadron leader is destroyed we award less experience
							(msnIncData gSource 'missionXP -50)
							(msnSetData gSource 'leaderLost True)
							(msnSetPlayerTarget gSource)
							(switch
								; Remember if player attacks squadron
								(and gPlayerShip (= aOrderGiver gPlayerShip))
									(msnSetData gSource 'betrayal True)

								; Otherwise, if player was not participating then the mission is now over
								(= (msnGetData gSource 'state) 'declined)
									(msnFailure gSource)
								)
							)

					(= (objGetID aObjDestroyed) (msnGetData gSource 'targetID))
						(switch
							; Bonus if the player destroys the stronghold
							(and gPlayerShip (= aOrderGiver gPlayerShip))
								(block Nil
									(msnIncData gSource 'missionXP 150)
									(typIncData &unidKorolovEscort; 'recordStrongholds)
									(msnSetData gSource 'commend True)
									(msnSuccess gSource)
									)

							; Check if the player was anywhere near the action
							(gr (objGetDistance aObjDestroyed gPlayerShip) 300)
								(block Nil
									(msnIncData gSource 'missionXP -50)
									(msnSetData gSource 'noHelp True)
									(msnSuccess gSource)
									)

							(msnSuccess gSource)
							)
					)
			</OnObjDestroyed>

			<OnObjDocked>
				; If player docks with station after leader is destroyed then mission is failure
				(if	(and
						(and gPlayerShip (= aObjDocked gPlayerShip))
						(= (objGetID aDockTarget) (msnGetProperty gSource 'ownerID))
						(not (msnGetProperty gSource 'isSuccess))
						(msnGetData gSource 'leaderLost)
						)
					(block Nil
						; If the player hasn't accepted the mission yet then force now to display debrief screen
						(if (= (msnGetData gSource 'state) 'declined)
							(msnAccept gSource)
							)
						(msnFailure gSource)
						)
					)
			</OnObjDocked>
		</Events>

		<Language>
			<Text id="Name">
				"Destroy Pirate stronghold"
			</Text>

			<Text id="Summary">
				(block Nil
					(cat
						"Destroy Pirate stronghold.\n\n"
						"System: " (sysGetName) "\n"
						"Payment: " (fmtCurrency 'credit (msnGetData gSource 'reward)) "\n"
						)
					)
			</Text>

			<Text id="Intro">
				(cat
					"As you enter the command center, you see several people gathered around a central display table.\n\n"
					"Assistant Director " (msnGetData gSource 'director) " approaches you, "
					"\"We're planning an operation to destroy the primary Charon stronghold in the system; we could use your help.\""
					)
			</Text>

			<Text id="Briefing">
				(if (msnGetProperty gSource 'isDeclined)
					(list
						{
							desc:	"\"Sorry, all freighters are grounded for now; we've deployed a squadron to attack the Charon stronghold in the system.\""
							acceptLabel: "\"[M]aybe I can help.\""
							declineLabel: "\"[C]ontinue.\""
							}
						)
					(cat
						"\"The stronghold is located in the outer system " (msnGetData gSource 'dist) " light-minutes from here. "
						"We're sending a squadron out to destroy it. We want you to join the squadron and follow them to the stronghold.\""
						)
					)
			</Text>

			<Text id="AcceptReply">
				(if (msnGetProperty gSource 'isDeclined)
					"\"Alright, we've uploaded the coordinates into your computer. The squadron will appreciate any help you can give them Good hunting!\""
					"\"Alright, hit the pirates as hard as you can and get back here in one piece. Good hunting!\""
					)
			</Text>

			<Text id="DeclineReply">
				(if (msnGetData gSource 'alreadyDeclined)
					"\"Why don't you help with the freighter maintenance until the combat pilots get back?\""
					"\"That's not too surprising. This is a dangerous mission and only the best pilots attempt it. Take care of yourself.\""
					)
			</Text>

			<Text id="InProgress">
				(if (msnGetProperty gSource 'isDeclined)
					"\"What are you doing here? Have you changed your mind again? Pull yourself together and go out there and fight!\""
					"\"What are you doing here? How can you abandon your squadron? Pull yourself together and go out there and fight!\""
					)
			</Text>
			<Text id="FailureDebrief">
				(if (msnGetData gSource 'betrayal)
					(cat "\"Either your poor marksmanship or deliberate malice destroyed the"
						" squadron fighting the pirates! You will never work for us again, captain.\""
						)
					(cat
						"\"What a disaster! If we can't shut down that Charon stronghold we're"
						" going to have to cut down on freighter traffic&#x97;at least until we"
						" can find some better pilots! You're dismissed.\""
						)
					)
			</Text>
			<Text id="SuccessDebrief">
				(switch
					(msnGetData gSource 'betrayal)
						(cat
							"\"What happened out there? You're supposed to shoot at the pirates, not everything "
							"that moves! At least the pirates are out of action for a while.\""
							)
					(msnGetData gSource 'noHelp)
						(cat
							"\"Welcome back captain. We managed to destroy the stronghold, but it appears you have some "
							"problems with basic navigation. Perhaps you should get you ships computer checked out.\""
							)
					(msnGetData gSource 'leaderLost)
						(cat
							"\"Good work captain. It will take a while for the pirates to rebuild their stronghold. "
							"But we lost some good people too.\""
							)
					(msnGetData gSource 'commend)
						(cat
							"\"Great flying, captain! It will take a while for the pirates to rebuild their stronghold. "
							"The squadron leader sends " (random '("his" "her")) " thanks for your help. "
							"We can all enjoy a bit of peace for a change.\""
							)
					(cat
						"\"Great flying, captain! It will take a while for the pirates to rebuild their stronghold. "
						"We can all enjoy a bit of peace for a change.\""
						)
					)
			</Text>
			<Text id="AlreadyDebriefed">
				"\"Welcome back! Thanks for the great work last time.\""
			</Text>

		</Language>

	</MissionType>

</TranscendenceModule>
