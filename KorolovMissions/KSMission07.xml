<?xml version="1.0" ?>

<TranscendenceModule>

	<!-- Mission: Kronosaurus ==================================================

	The Kronosaurus hunts the player

	EXTRA DATA


	======================================================================== -->

	<MissionType UNID="&msKronosaurus;"
			name=			"The Kronosaurus"
			attributes=		"special"
			>
		<Events>
			<OnCreate>
				; No mission if the Kronosaurus is unavailable
				(if (typGetGlobalData &scCharonFrigateKronosaurus; 'status)
					(msnDestroy gSource)
					)
			</OnCreate>

			<OnAccepted>
				(block (kronosaurus korStation)
					; Create the Kronosaurus
					(setq kronosaurus
						(sysCreateShip
							&scCharonFrigateKronosaurus;
							(sysFindObject gPlayerShip "GR -uncharted;")
							&svPirates;
							)
						)

					; Order the Kronosaurus to attack the player
					(objFireEvent kronosaurus "OrderHuntPlayer")
					(msnSetData gSource 'targetID (objGetID kronosaurus))
					(msnRegisterForEvents gSource kronosaurus)
					(msnRegisterForEvents gSource gPlayerShip)

					; We debrief at the nearest Korolov station
					(if (setq korStation (sysFindObject Nil "TN +korolovShipping;"))
						(msnSetProperty gSource 'debrieferID (objGetID korStation))
						)
					)
			</OnAccepted>

			<OnSetPlayerTarget>
			</OnSetPlayerTarget>

			<OnObjDestroyed>
				(switch
					(= aDestroyReason 'enteredStargate)
						Nil

					(= aObjDestroyed gPlayerShip)
						(block Nil
							(msnFailure gSource)
							; suppress the debrief until we add a failure screen
							; probably only possible if player has insurance
							(msnSetProperty gSource 'isDebriefed True)
							)

					(= (objGetID aObjDestroyed) (msnGetData gSource 'targetID))
						(block Nil
							(msnSetData gSource 'destroyedByPlayer (= aOrderGiver gPlayerShip))
							(msnSetData gSource 'destroyedBy (objGetName aOrderGiver 0x04))
							(msnSuccess gSource)
							)
					)
			</OnObjDestroyed>

			<OnGetNextScreen>
				(if (and (= aScreenType 'SuccessFollowUp) (msnGetData gSource 'destroyedByPlayer))
					{
						nextScreen: &dsWingmanEncounter;
						nextScreenData: {wingman: &scWolfenVolkov;}
						}
					)
			</OnGetNextScreen>

			<OnReward>
				(if (msnGetData gSource 'destroyedByPlayer)
					(block Nil
						(typSetGlobalData &unidKorolovEscort; 'level 4)
						(typSetGlobalData &unidKorolovEscort; 'xp 5000)
						)
					)
			</OnReward>

			<OnGlobalSystemStarted>
				; We can debrief at any Korolov so reset debrieferID everytime we change system
				; Note - this mean we can't have more than one Korolov station per system, but
				; we could use GetGlobalDockScreen instead
				(block (
					(theMission (@ (msnFind "a +unid:&msKronosaurus;;") 0))
					(korStation (sysFindObject Nil "TN +korolovShipping;"))
					)
					(if (and theMission korStation)
						(msnSetProperty theMission 'debrieferID (objGetID korStation))
						)
					)
			</OnGlobalSystemStarted>

			<GetGlobalAchievements>
				(block (
					(theMission (@ (msnFind "r +unid:&msKronosaurus;; +property:isCompleted;") 0))
					)
					(switch
						(not theMission)
							Nil

						(msnGetProperty theMission 'isFailure)
							(list (list
								"Defeated by the Kronosaurus"
								Nil
								"achievements &amp; regrets"
								))

						(and (msnGetProperty theMission 'isSuccess) (msnGetData theMission 'destroyedByPlayer))
							(list (list
								"Defeated the Kronosaurus"
								Nil
								"achievements &amp; regrets"
								))

						(msnGetProperty theMission 'isSuccess)
							(list (list
								"Evaded the Kronosaurus"
								Nil
								"achievements &amp; regrets"
								))

						Nil
						)
					)
			</GetGlobalAchievements>

			<GetRumors>
				(switch
					; Kronosaurus is in system
					(and
						(= (msnGetProperty gSource 'nodeID) (sysGetNode))
						(msnGetProperty gSource 'isActive)
						(not (msnGetData gSource 'destroyedBy))
						)
						(msnTranslate gSource "Rumor:inSystem")

					; Kronosaurus is destroyed (don't care if it was player or other)
					(and
						(= (msnGetProperty gSource 'nodeID) (sysGetNode))
						(msnGetProperty gSource 'isSuccess)
						)
						(msnTranslate gSource "Rumor:isSuccess")

					Nil
					)
			</GetRumors>
		</Events>

		<Language>
			<Text id="Name">
				"Defeat the Kronosaurus"
			</Text>
			<Text id="Summary">
				(cat
					"By destroying several Charon frigates you have alarmed the pirates. "
					"They're sending the Kronosaurus after you! It is the deadliest frigate "
					"in the Charon fleet and it will not stop until you are destroyed.\n\n"

					"System: " (sysGetName) "\n"
					"Payment: None"
					)
			</Text>
			<Text id="SuccessMsg">
				"Mission complete!"
			</Text>
			<Text id="SuccessDebrief">
				(if (msnGetData gSource 'destroyedByPlayer)
					(msnTranslate gSource "SuccessDebrief:player")
					(msnTranslate gSource "SuccessDebrief:other")
					)
			</Text>
			<Text id="SuccessDebrief:player">
				(cat
					"A crowd gathers around you as you enter the docking bay. "
					"Assistant Director "
					(objGetData (objGetObjByID (msnGetProperty gSource 'debrieferID)) 'assistantDirector)
					" shakes your hand:\n\n"
					"\"I never thought I'd see the day! You've destroyed the Kronosaurus! The deadliest ship in the Charon fleet! "
					"This day and your accomplishments will be remembered always.\""
					)
			</Text>
			<Text id="SuccessDebrief:other">
				(cat
					"There is a crowd celebrating in the main hall. "
					"One of the freighter captains approaches you:\n\n"
					"\"Did you hear, the Kronosaurus has been destroyed by "
					(msnGetData gSource 'destroyedBy) "!\""
					)
			</Text>
			<Text id="Rumor:inSystem">
				{
					desc: (cat
						"\"Watch out for the Charon pirates. I hear that they're furious with "
						"someone in system and they've sent the Kronosaurus in to kill them.\""
						)
					priority:10
					}
			</Text>
			<Text id="Rumor:isSuccess">
				{
					desc: (cat
						"\"Did you hear the news? The Charon pirates sent the Kronosaurus to "
						(sysGetName) " but it is was destroyed by "
						(msnGetData gSource 'destroyedBy) "!\""
						)
					priority:2
					}
			</Text>

		</Language>
	</MissionType>

<!-- GLOBALS -->

	<Globals>
		(block Nil
			(setq chrDeployKronosaurus (lambda ()
				(block (theMission)
					(if (setq theMission (msnCreate &msKronosaurus; Nil))
						(msnAccept theMission)
						)
					)
				))
			)
	</Globals>

</TranscendenceModule>
